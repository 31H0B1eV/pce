#*****************************************************************************
#* pce                                                                       *
#*****************************************************************************

#*****************************************************************************
#* File name:     configure.in                                               *
#* Created:       2002-05-20 by Hampa Hug <hampa@hampa.ch>                   *
#* Last modified: 2003-09-04 by Hampa Hug <hampa@hampa.ch>                   *
#* Copyright:     (C) 2002-2003 by Hampa Hug <hampa@hampa.ch>                *
#*****************************************************************************

#*****************************************************************************
#* This program is free software. You can redistribute it and / or modify it *
#* under the terms of the GNU General Public License version 2 as  published *
#* by the Free Software Foundation.                                          *
#*                                                                           *
#* This program is distributed in the hope  that  it  will  be  useful,  but *
#* WITHOUT  ANY   WARRANTY,   without   even   the   implied   warranty   of *
#* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU  General *
#* Public License for more details.                                          *
#*****************************************************************************

# $Id: configure.in,v 1.22 2003/09/13 18:12:16 hampa Exp $


AC_PREREQ(2.50)

AC_INIT(Makefile.in)
AC_CONFIG_HEADER(src/config.h)


dnl -----------------------------------------------------------------------

AC_MSG_CHECKING([pce version])
PCE_VERSION_MAJ=0
PCE_VERSION_MIN=1
PCE_VERSION_MIC=2
PCE_VERSION_STR="$PCE_VERSION_MAJ.$PCE_VERSION_MIN.$PCE_VERSION_MIC"
AC_SUBST(PCE_VERSION_MAJ)
AC_SUBST(PCE_VERSION_MIN)
AC_SUBST(PCE_VERSION_MIC)
AC_SUBST(PCE_VERSION_STR)
AC_DEFINE_UNQUOTED(PCE_VERSION_MAJ, $PCE_VERSION_MAJ)
AC_DEFINE_UNQUOTED(PCE_VERSION_MIN, $PCE_VERSION_MIN)
AC_DEFINE_UNQUOTED(PCE_VERSION_MIC, $PCE_VERSION_MIC)
AC_DEFINE_UNQUOTED(PCE_VERSION_STR, "$PCE_VERSION_STR")
AC_MSG_RESULT($PCE_VERSION_STR)

PCE_CFG_DATE=`date +%Y-%m-%d`
PCE_CFG_TIME=`date +%T`
AC_SUBST(PCE_CFG_DATE)
AC_SUBST(PCE_CFG_TIME)
AC_DEFINE_UNQUOTED(PCE_CFG_DATE, "$PCE_CFG_DATE")
AC_DEFINE_UNQUOTED(PCE_CFG_TIME, "$PCE_CFG_TIME")


AC_ARG_WITH(x11,
  AC_HELP_STRING([--with-x11], [Use the X11 terminal]),
  [
  	PCE_X11_USE="$withval"
  ],
  [
  	PCE_X11_USE=no
  ]
)
if test "x$PCE_X11_USE" = "xyes" ; then
	PCE_X11_USE=1
	PCE_X11_CCF="-I/usr/X11R6/include"
	PCE_X11_LDF="-L/usr/X11R6/lib -lX11"
	AC_DEFINE(PCE_X11_USE)
else
	PCE_X11_USE=0
fi
AC_SUBST(PCE_X11_USE)
AC_SUBST(PCE_X11_CCF)
AC_SUBST(PCE_X11_LDF)


AC_ARG_WITH(host-clock,
  AC_HELP_STRING([--with-host-clock=clock], [Set the host clock speed in Hz]),
  [
  	PCE_HOST_FOSC="$withval"
  ],
  [
  	PCE_HOST_FOSC=4772393
  ]
)
AC_SUBST(PCE_HOST_FOSC)
AC_DEFINE_UNQUOTED(PCE_HOST_FOSC, $PCE_HOST_FOSC)


PCE_HAVE_TSC=0
AC_ARG_ENABLE(tsc,
  AC_HELP_STRING([--enable-tsc], [Use Pentium TSC]),
  [
  	if test "x$enableval" = "xyes" ; then
  		PCE_HAVE_TSC=1
  	fi
  ],
  [
	case "`uname -m`" in
	i?86)
		PCE_HAVE_TSC=1
		;;
	esac
  ]
)
AC_SUBST(PCE_HAVE_TSC)
if test $PCE_HAVE_TSC -eq 1 ; then
	AC_DEFINE(PCE_HAVE_TSC)
fi


AC_MSG_CHECKING([host OS])
os=`uname -s`
case "$os" in
[[Ll]]inux)
	AC_DEFINE(PCE_HOST_LINUX)
	AC_MSG_RESULT([Linux])
	;;
*)
	AC_MSG_RESULT([unknown ($os)])
	;;
esac


dnl -----------------------------------------------------------------------
dnl Checks for programs.

AC_PROG_CC
AC_PROG_INSTALL
AC_PATH_PROG(AR, ar, ar)
AC_PATH_PROG(NASM, nasm,)
AC_PROG_MAKE_SET
AC_PROG_LN_S


if test -x "$NASM" ; then
	PCE_HAVE_NASM=1
else
	PCE_HAVE_NASM=0
fi
AC_SUBST(PCE_HAVE_NASM)


dnl -----------------------------------------------------------------------
dnl Checks for header files.

AC_HEADER_STDC
AC_CHECK_HEADERS(limits.h unistd.h fcntl.h)


dnl -----------------------------------------------------------------------
dnl Checks for libraries


eval "pce_etcdir=$sysconfdir"
eval "pce_etcdir=$pce_etcdir"

eval "pce_datdir=$datadir"
eval "pce_datdir=$pce_datdir"

PCE_DIR_ETC=$pce_etcdir
PCE_DIR_DATA=$pce_datdir

AC_SUBST(PCE_DIR_ETC)
AC_SUBST(PCE_DIR_DATA)
AC_DEFINE_UNQUOTED(PCE_DIR_ETC, "$pce_etcdir")
AC_DEFINE_UNQUOTED(PCE_DIR_DATA, "$pce_datdir")


AC_OUTPUT(
  Makefile.inc
  Makefile
  src/Makefile
  src/chipset/Makefile
  src/e8086/Makefile
  src/ibmpc/bios/Makefile
  src/ibmpc/Makefile
  src/libini/Makefile
  src/pceutils/Makefile
  src/terminal/Makefile
  src/config.inc
)


dnl -----------------------------------------------------------------------

term="vt100"

if test "x$PCE_X11_USE" = "x1" ; then
	term="$term x11"
fi

eval "pce_bindir=$bindir"
eval "pce_bindir=$pce_bindir"

eval "pce_libdir=$libdir"
eval "pce_libdir=$pce_libdir"

eval "pce_mandir=$mandir"
eval "pce_mandir=$pce_mandir"


echo ""
echo "pce $PCE_VERSION_STR is now configured:"

echo "  CC:                    $CC"
echo "  CFLAGS:                \"$CFLAGS\""
echo "  LDFLAGS:               \"$LDFLAGS\""
echo ""
echo "  prefix:                $prefix"
echo "  bindir:                $pce_bindir"
echo "  etcdir:                $pce_etcdir"
echo "  libdir:                $pce_libdir"
echo "  mandir:                $pce_mandir"
echo "  datadir:               $pce_datdir"
echo ""
echo "  Terminals:             $term"

if test $PCE_HAVE_NASM -eq 0 ; then
	echo "  Build BIOS:            no"
else
	echo "  Build BIOS:            yes"
fi

echo "  Host clock:            $PCE_HOST_FOSC"
echo "  Use Pentium TSC:       $PCE_HAVE_TSC"
